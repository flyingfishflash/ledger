plugins {
    id 'com.diffplug.spotless' version '5.8.2'
    id 'fr.brouillard.oss.gradle.jgitver' version '0.10.0-rc03'
    id 'com.geoffgranum.gradle-conventional-changelog' version '0.3.1'
}

subprojects {
    ext {
        googleJavaFormatVersion = "1.9"
    }
}

task cleanChangeLog {
    outputs.upToDateWhen { false }
    doLast {
        exec {
            commandLine "sh", "-c", "[ -e \"CHANGELOG.md\" ] && sed -i '/## Docs/,\$d' CHANGELOG.md || echo * CHANGELOG.md doesn\\'t exist"
        }
    }
}

task writeVersion {
    outputs.upToDateWhen { false }
    doLast {
        new File(projectDir, "version").text = version
    }
}

build {
    finalizedBy writeVersion
}

changelog {
    append 'false'
    appName rootProject.name
    file 'CHANGELOG.md'
    //match '^fix|^feat|^fix|^perf|^refactor|^docs|BREAKING' //default
    match '^fix|^feat|^fix|^chore|^docs|^style|^refactor|^perf|^test|BREAKING'
    repoUrl 'https://github.com/rollenwiese/ledger'
    trackerUrl 'https://github.com/rollenwiese/ledger'
    versionNum version.toString()
}

spotless {
    format 'yaml', {
        target '*.yml'
        prettier()
    }

    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
}
