plugins {
    id 'java'
    id 'org.springframework.boot' version '2.3.4.RELEASE'
    id 'fr.brouillard.oss.gradle.jgitver' version '0.10.0-rc03'
    id "com.diffplug.spotless" version "5.6.1"
    id "com.github.node-gradle.node" version "3.0.0-rc3"
}

group = 'net.flyingfishflash'
description = 'Ledger'
sourceCompatibility = '14'

ext {
    springBootVersion = "2.3.5.RELEASE"
    springSessionVersion = "2.4.0"
    googleJavaFormatVersion = "1.9"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()

    maven {
        url = uri('https://jitpack.io')
    }

    maven {
        url = uri('https://oss.jfrog.org/artifactory/oss-snapshot-local/')
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-websocket:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-validation:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-security:$springBootVersion"
    implementation "org.springframework.session:spring-session-core:$springSessionVersion"
    implementation "org.springframework.session:spring-session-jdbc:$springSessionVersion"
    implementation 'jakarta.validation:jakarta.validation-api:2.0.2'
    implementation 'io.springfox:springfox-boot-starter:3.0.0-SNAPSHOT'
    implementation 'com.github.eXsio:nestedj:5.0.3'
    implementation 'com.h2database:h2:1.4.200'
    implementation 'org.postgresql:postgresql:42.2.18'
    implementation 'org.javamoney:moneta:1.4.2'
    implementation 'javax.interceptor:javax.interceptor-api:1.2.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.2'
    implementation 'org.zalando:jackson-datatype-money:1.2.0'
    implementation 'org.flywaydb:flyway-core:7.0.1'
    runtimeOnly 'javax.xml.bind:jaxb-api:2.3.1'
    testImplementation 'org.springframework.security:spring-security-test:5.4.1'
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springBootVersion" exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    filter {
        includeTestsMatching "net.flyingfishflash.ledger.*.unit.*"
    }
}

task integrationTests(type: Test) {
    systemProperties System.properties
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
    }

    filter {
        includeTestsMatching "net.flyingfishflash.ledger.*.integration.*"
    }
}

task buildFrontEnd(type: NpmTask) {
    dependsOn(npmInstall)
    args = ['run', 'buildProd']
}

task releaseNotes(type: NpmTask) {
    args = ['run', 'release', '--', '--dry-run']
}

task finalize {
    doLast {
        println(version)
    }
}

processResources {
    from("src/main/frontend/dist/ledger-frontend/") {
        into("static")
    }

//    rename("application-${profile}.properties", "application.properties")
//    if (profile == "prod") {
//        dependsOn(buildWeb)
//        from("${spa}/dist/notes") {
//            into("static")
//        }
//    }
}

bootJar {
    dependsOn(buildFrontEnd)
    archiveFileName = "ledger.jar"
}

bootRun {
    jvmArgs = ["-Duser.timezone=UTC"]
}

build {
    finalizedBy(finalize)
    dependsOn buildFrontEnd
}

integrationTests {
    mustRunAfter(test)
}

node {
    download = true
    version = "15.2.0"
    nodeProjectDir = file("${project.projectDir}/src/main/frontend/")
}

spotless {
    format 'misc', {
        target '*.gradle', '*.md', '.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }

    java {
        googleJavaFormat(googleJavaFormatVersion)
        trimTrailingWhitespace()
        importOrder('java', 'javax', 'com', 'org', 'net.flyingfishflash.ledger')
    }

    springBoot {
        buildInfo()
    }
}
